# Prolog desde 0
## √Årboles binarios balanceados

Vamos a aprender a definir √°rboles binarios balanceados trabajando el pensamiento declarativo y la idea del principio de inducci√≥n. 
Con este ejercicio, aprenderemos a identificar si un √°rbol binario es balanceado o no.
Nos apoyaremos en el siguiente video: [2.10 Programaci√≥n L√≥gica con Prolog: √Årboles balanceados (Nueva versi√≥n 4K!)üå≥üå≥‚öñ
](https://www.youtube.com/watch?v=JkBlucdXFYg&list=PL_d-XKRO_5G_4k1l6Dz81JhyLnyXRkEsP&index=20)

Para ello, definiremos los predicados **balanceado** y **altura**, siendo este √∫ltimo utilizado en el de balanceado.
Vamos a ver **balanceado**.

Un √°rbol es balanceado si, **para todo nodo**, la diferencia entre la altura del hijo Izquierda y la altura del hijo derecha es como m√°ximo 1 en valor absoluto. O lo que es lo mismo, si la altura del hijo izquierdo y el derecho se diferencian como m√°ximo en 1 y el √°rbol de la izquierda y de la derecha es balanceado.
Por definici√≥n, el √°rbol nulo es balanceado.

Para comprobar si el √°rbol es balanceado usaremos el predicado **balanceado**
```
/*
balanceado(+Arbol_Binario)
  es cierto si, para todo nodo, la diferencia entre
  la altura del Hijo Izquierda y la altura del 
  Hijo derecha es como m√°ximo 1 en valor absoluto.
*/
```
Pasamos a implementarlo. utilizando el **Principio de Inducci√≥n**. √âste nos dice:

1. P(n0) 
Necesitamos que la propiedad sea cierta para el √°rbol m√°s peque√±o. En este caso el m√°s peque√±o es el √°rbol nulo. 
El √°rbol nulo es balanceado y lo escribiremos con el siguiente predicado:
balanceado(nil).
Esto nos sirve para identificar cu√°ndo un √°rbol es nulo.

2. ‚àÄ n > no, P(n-1)-> P(n)
La segunda parte del principio de inducci√≥n nos dice que tenemos que preguntarle al problema quitando un elemento. 
En este caso lo aplicamos a la izquierda y a la derecha y lo escribimos as√≠:

Definimos un √°rbol 'a' sin etiqueta, que indicamos con una variable an√≥nima '_', y sus hijos, el izquierdo y el derecho.
```
  balanceado(a(_, HI, HD)):-
```
y comprobamos las alturas
```
  altura(HI, AI),
  altura(HD, AD),
  ```
desp√∫es se implementar√° el predicado **altura**.

Ahora comprobaremos que el valor abosluto de la diferencia de altura entre el hijo de la izquierda
y el de la derecha sea menor o igual que uno.
```
Abs_dif is abs(Dif),
  Abs_dif =< 1,
```

A continuaci√≥n comprobamos que sea balanceado el hijo de la izquierda y el de la derecha
```
balanceado(HI),
  balanceado(HD).
```
que permite comprobar las diferencias entre las alturas para todo el √°rbol.

Nos queda implementar el predicado **altura** del √°rbol, como indicamos anteriormente.
```
/* altura(+Arbol_binario, -Altura)
      es cierto si Altura unifica con la
   altura de Arbol_binario.
*/  
```
Volvemos a utilizar el principio de inducci√≥n.
```
P(no) 
```
Necesitamos la altura del √°rbol m√°s peque√±o, la del √°rbol nulo, que ser√° cero.
```
altura(nil,0).
```
```
‚àÄ n > no, P(n-1)-> P(n)
```
En la segunda parte del principio de inducci√≥n no vamos a quitar uno, lo que haremos es preguntar a la izquierda y a la derecha.
El problema m√°s peque√±o lo hacemos quitando todos los elementos de una parte primero, de la izquierda, y despu√©s de la derecha.

Definimos el predicado altura del √°rbol, en el que no pondremos etiqueta, definimos los hijos y calculamos las dos alturas. La altura del √°rbol ser√° el m√°ximo de las dos alturas 
mas uno, para contar la ra√≠z del √°rbol. 
```
  altura(a(_, HI, HD), R):-
  altura(HI, AI),
  altura(HD, AD),
  A is max(AI, AD),
  R is A+1.
```
El c√≥digo quedar√≠a ya de la siguiente forma:
```
/*
balanceado(+Arbol_Binario)
  es cierto si, para todo nodo, la diferencia entre
  la altura del Hijo Izquierda y la altura del 
  Hijo derecha es como m√°ximo 1 en valor absoluto.
*/

balanceado(nil).
balanceado(a(_, HI, HD)):-
  altura(HI, AI),
  altura(HD, AD),
  Dif is AI-AD,
  Abs_dif is abs(Dif),
  Abs_dif =< 1,
  balanceado(HI),
  balanceado(HD).

/* altura(+Arbol_binario, -Altura)
      es cierto si Altura unifica con la
   altura de Arbol_binario.
*/  
altura(nil,0).
altura(a(_, HI, HD), R):-
  altura(HI, AI),
  altura(HD, AD),
  A is max(AI, AD),
  R is A+1.
```
  
Probaremos la implentaci√≥n con los siguientes √°rboles:
```
   1) 1         2) 1
     / \          / \
    2   3	 2   5
                / \
	       3   4
```

No balanceados
```
   3) 1         4) 1
     /            / \
    2    	 2   5
   / \          /     \
  3   4        3       6  
       \      /         \
        5    4           7
```
que se definir√≠an de la siguiente forma:
```
1)arbol1(a(1,a(2,nil,nil),a(3,nil,nil))).  
2)arbol2(a(1,a(2,a(3,nil,nil),a(4,nil,nil)),a(5,nil,nil))).
3)arbol3(a(1,a(2,a(3,nil,nil),a(4,nil,a(5,nil,nil))),nil)).
4)arbol4(a(1,a(2,a(3,a(4,nil,nil), nil),nil), a(5,nil,a(6,nil,a(7,nil,nil))))).
?- arbol1(A), balanceado(A)
```

 
